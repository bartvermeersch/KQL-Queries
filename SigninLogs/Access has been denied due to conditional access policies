Title: Access has been blocked due to conditional access policies

Description:
This KQL query looks to see if a user has been denied access due to the conditional access policies.

Data Source: SigninLogs

Tactic: Initial Access

KQL Query:

// KQL query to find out which user has been denied due to conditional access policies
// Twitter: @DebugPrivilege
let starttime = 14d;
let endtime = 0d;
SigninLogs
| where TimeGenerated between(ago(starttime)..ago(endtime))
| where OperationName == "Sign-in activity" 
and ResultDescription =~ "Access has been blocked due to conditional access policies." and ResultType == "53003"
| extend City = tostring(LocationDetails.city) 
| extend State = tostring(LocationDetails.state)
| summarize StartTimeUtc = min(TimeGenerated), EndTimeUtc = max(TimeGenerated), DistinctIPCount = dcount(IPAddress), any(AppDisplayName), 
AttemptCount = count() by Identity, Location, City, State, ClientAppUsed, IPAddress
| project-rename AppDisplayName = any_AppDisplayName
| sort by StartTimeUtc desc

Updated: 7-5-2020
